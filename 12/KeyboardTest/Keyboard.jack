// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int pos;

    /** Initializes the keyboard. */
    function void init() {
        let pos = 24576;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(pos);
    }

    function char getChar(){
        var char key;
        var String s;

        let key = Keyboard.keyPressed();
        while(key = 0){
            let key = Keyboard.keyPressed();
        }

        while(~(Keyboard.keyPressed() = 0)){  //键被按下

        }
        return key;
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char key;
        do Output.printChar(0);
        let key = Keyboard.getChar();
        do Output.backSpace();
        do Output.printChar(key);
        return key;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var char key;
        var String s;
        let s = String.new(10);

        do Output.printString(message);
        do Output.printChar(0);

        let key = 129;
        while(~(key = 128)){
            if((key = 129) & (~(s.length() = 0))){ //退格
                do s.eraseLastChar();
                do Output.backSpace();
                do Output.backSpace();
                do Output.printChar(0);
            }else{
                if(~(key = 129)){
                    do s.appendChar(key);
                }
            }
            let key = Keyboard.getChar();
            if(~(key=129)){
                do Output.backSpace();
                do Output.printChar(key);
                if(~(key=128)){
                    do Output.printChar(0);
                }
            }
        }
        return s;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String s;
        let s = Keyboard.readLine(message);
        return s.intValue();
    }
}
